;-------------------------------------------------------------------------;
.include "snes.inc"
.include "snes_decompress.inc"
.include "snes_zvars.inc"
;-------------------------------------------------------------------------;
.export DoTaurusLogo
;-------------------------------------------------------------------------;


;-------------------------------------------------------------------------;
RAM_M7B	= 11e4h
RAM_M7C	= 1000h
;-------------------------------------------------------------------------;


;-------------------------------------------------------------------------;
bg1_vofs_index	=	0408h
end_flag	=	bg1_vofs_index+2
fade_in_delay	=	end_flag+1
fi_inidisp	=	fade_in_delay+1
fo_inidisp	=	fi_inidisp+1
loop1_timer	=	fo_inidisp+1
m7c_index	=	loop1_timer+2
m7x_index	=	m7c_index+2
m7y_index	=	m7x_index+2
shadow_m7a	=	m7y_index+2
zoom_in1	=	shadow_m7a+2

copy_m7_map	=	0500h
map_entry	=	copy_m7_map+1
tile_xpos	=	map_entry+2
;-------------------------------------------------------------------------;


;/////////////////////////////////////////////////////////////////////////;
.code
;/////////////////////////////////////////////////////////////////////////;


;-------------------------------------------------------------------------;
	.a8
	.i16
;-------------------------------------------------------------------------;


;:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::;
DoTaurusLogo:
;:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::;

	rep	#10h
	sep	#20h

	sei	

	lda	#BGMODE_7
	sta	REG_BGMODE

	stz	REG_BG1SC
	stz	REG_BG12NBA

	lda	#TM_BG1
	sta	REG_TM

	DoCopyPalette	M7PAL, 0, 32

	rep	#30h

	lda	#(HDMA_M7C_END-HDMA_M7C)-1	; bytes to transfer
	ldx	#HDMA_M7C			; src address
	ldy	#RAM_M7C			; destination address
	mvn	80h,00h				; dest bank, src bank
	;	^^-- DBR = this value

	sep	#20h

	lda	#DMAP_XFER_MODE_2
	sta	REG_DMAP0
	sta	REG_DMAP1

	lda	#<REG_M7B
	sta	REG_BBAD0
	ldx	#RAM_M7B
	stx	REG_A1T0L
	stz	REG_A1B0

	lda	#<REG_M7C
	sta	REG_BBAD1
	ldx	#RAM_M7C
	stx	REG_A1T1L
	stz	REG_A1B1

	lda	#%11
	sta	REG_HDMAEN

	stz	end_flag
	stz	fade_in_delay
	stz	fi_inidisp
	stz	tile_xpos
	stz	zoom_in1

	ldx	#01f0h
	stx	shadow_m7a

	lda	#97h
	sta	REG_M7Y
	stz	REG_M7Y

	lda	#0f0h
	sta	REG_M7A
	sta	REG_M7D

	lda	#01h
	sta	REG_M7A
	sta	REG_M7D
	stz	REG_M7B
	stz	REG_M7B
	stz	REG_M7C
	stz	REG_M7C
	stz	REG_M7SEL
	stz	$36
	sta	copy_m7_map

	lda	#80h
	sta	REG_M7X
	stz	REG_M7X
	sta	REG_VMAIN

	ldx	#0000h
	stx	bg1_vofs_index		; as long as $0 is loaded...
	stx	loop1_timer
	stx	map_entry
	stx	m7c_index
	stx	REG_VMADDL
;-------------------------------------------------------------------------;
:	phx
	jsr	Copy256x256Mode7Map
	plx
	lda	f:M7PIC,x
	sta	REG_VMDATAH
	inx
	cpx	#4000h
	bne	:-
;-------------------------------------------------------------------------;
	stz	REG_INIDISP
	lda	#NMI_ON|NMI_JOYPAD
	sta	REG_NMITIMEN

	cli	

	lda	#0fh
	sta	fo_inidisp
;-------------------------------------------------------------------------;
loopy1:	wai

	jsr	FadeInTimer
	jsr	Wobble
	jsr	ZoomIn1

	ldy	loop1_timer
	cpy	#0200h
	beq	:+
;-------------------------------------------------------------------------;
	iny	
	sty	loop1_timer
	bra	loopy1
;-------------------------------------------------------------------------;
loopy2:	wai
;-------------------------------------------------------------------------;
:	jsr	ZoomIn2
	jsr	Wobble
	jsr	MosaicFadeOut
;-------------------------------------------------------------------------;
	lda	end_flag
	bne	:+
;-------------------------------------------------------------------------;
	bra	loopy2
;-------------------------------------------------------------------------;
:	wai
	bra	:-


;=========================================================================;
FadeIn:
;=========================================================================;
	lda	fi_inidisp
	cmp	#1eh
	beq	:+
;-------------------------------------------------------------------------;
	ina
	sta	fi_inidisp
	lsr
	sta	REG_INIDISP
;-------------------------------------------------------------------------;
:	rts	
;=========================================================================;
FadeInTimer:
;=========================================================================;
	lda	fade_in_delay
	cmp	#03h
	beq	:+
;-------------------------------------------------------------------------;
	ina
	sta	fade_in_delay
	rts
;-------------------------------------------------------------------------;
:	stz	fade_in_delay
	jsr	FadeIn
	rts	
;=========================================================================;
MosaicFadeOut:
;=========================================================================:
	lda	$36
	cmp	#10h
	beq	:+
;-------------------------------------------------------------------------;
	ina
	sta	$36
	rts	
;-------------------------------------------------------------------------;
:	lda	fo_inidisp
	beq	:+
;-------------------------------------------------------------------------;
	dea
	sta	fo_inidisp
	sta	REG_INIDISP
	lda	#10h
	sec	
	sbc	fo_inidisp
	asl
	asl
	asl
	asl
	ora	#01h
	sta	REG_MOSAIC
	rts	
;-------------------------------------------------------------------------;
:	lda	#01h
	sta	end_flag
	rts
;=========================================================================;
Wobble:
;=========================================================================;
	ldy	m7c_index
:	lda	LIST_M7C,y
	bne	:+
;-------------------------------------------------------------------------;
	ldy	#0000h
	bra	:-
;-------------------------------------------------------------------------;
:	sta	m7c_index
	sta	RAM_M7C
	lda	#22h
	sec
	sbc	m7c_index
	sta	RAM_M7B
	iny
	sty	m7c_index
	ldy	m7x_index
;-------------------------------------------------------------------------;
:	lda	LIST_M7XY,y
	bne	:+
;-------------------------------------------------------------------------;
	ldy	#0000h
	bra	:-
;-------------------------------------------------------------------------;
:	sta	REG_M7X
	stz	REG_M7X
	lsr
	lsr
	lsr
	lsr
	clc
	adc	RAM_M7B
	sta	RAM_M7B
	iny
	iny
	sty	m7x_index
	ldy	m7y_index
;-------------------------------------------------------------------------;
:	lda	LIST_M7XY,y
	bne	:+
;-------------------------------------------------------------------------;
	ldy	#0000h
	bra	:-
;-------------------------------------------------------------------------;
:	sta	REG_M7Y
	stz	REG_M7Y
	iny
	sty	m7y_index
	ldy	bg1_vofs_index
;-------------------------------------------------------------------------;
:	lda	LIST_BG1VOFS,y
	bne	:+
;-------------------------------------------------------------------------;
	ldy	#0000h
	bra	:-
;-------------------------------------------------------------------------;
:	lsr
	clc
	adc	#80h
	sta	REG_BG1VOFS
	lda	#0ffh
	sta	REG_BG1VOFS
	iny
	sty	bg1_vofs_index
	lda	#20h
	sta	REG_M7Y
	stz	REG_M7Y
	stz	REG_BG1HOFS
	stz	REG_BG1HOFS
:	rts
;=========================================================================;
ZoomIn1:
;=========================================================================;
	lda	zoom_in1
	bne	:-
;-------------------------------------------------------------------------;
	rep	#20h

	lda	shadow_m7a
	cmp	#0100h
	beq	:+
;-------------------------------------------------------------------------;
	dea
	dea
	sta	shadow_m7a

	sep	#20h

	sta	REG_M7A
	xba	
	and	#01h
	sta	REG_M7A
	xba	
	sta	REG_M7D
	xba	
	sta	REG_M7D
	rts	
;-------------------------------------------------------------------------;
:	sep	#20h

	lda	#01h
	sta	zoom_in1
	rts
;=========================================================================;
ZoomIn2:
;=========================================================================;
	rep	#20h

	lda	shadow_m7a
	cmp	#02h
	beq	:+
;-------------------------------------------------------------------------;
	dea
	dea
	sta	shadow_m7a

	sep	#20h

	sta	$36
	sta	REG_M7A
	xba	
	and	#01h
	sta	REG_M7A
	xba	
	sta	REG_M7D
	xba	
	sta	REG_M7D
;-------------------------------------------------------------------------;
:	sep	#20h
	rts


;=========================================================================;
Copy256x256Mode7Map:
;=========================================================================;
	lda	copy_m7_map
	bne	cmp21h
;-------------------------------------------------------------------------;
cmp61h:	inc	tile_xpos
	lda	tile_xpos
	cmp	#61h
	bne	zvmdl
;-------------------------------------------------------------------------;
	stz	tile_xpos
	lda	#01h
	sta	copy_m7_map
;-------------------------------------------------------------------------;
cmp21h:	inc	tile_xpos
	lda	tile_xpos
	cmp	#21h
	bne	cmpmap
;-------------------------------------------------------------------------;
	stz	tile_xpos
	stz	copy_m7_map
	bra	cmp61h
;-------------------------------------------------------------------------;
zvmdl:	stz	REG_VMDATAL
	rts
;-------------------------------------------------------------------------;
cmpmap:	ldx	map_entry
	cpx	#0400h		; only a 256x256 map
	beq	zvmdl
;-------------------------------------------------------------------------;
	lda	f:M7MAP,x	; get map data
	sta	REG_VMDATAL
	inx
	stx	map_entry
	rts
;-------------------------------------------------------------------------;


;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
HDMA_M7C:
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
	.byte	$08,$01,$00,$02,$02,$00,$01,$04,$00
	.byte	$01,$06,$00,$01,$08,$00,$01,$0a,$00
	.byte	$02,$0c,$00,$01,$0e,$00,$01,$10,$00
	.byte	$01,$12,$00,$01,$14,$00,$02,$16,$00
	.byte	$01,$18,$00,$01,$1a,$00,$01,$1c,$00
	.byte	$01,$1e,$00,$02,$20,$00,$01,$22,$00
	.byte	$01,$24,$00,$01,$26,$00,$01,$28,$00
	.byte	$02,$2a,$00,$01,$2c,$00,$01,$2e,$00
	.byte	$01,$30,$00,$01,$32,$00,$02,$34,$00
	.byte	$01,$36,$00,$01,$38,$00,$01,$3a,$00
	.byte	$01,$3c,$00,$02,$3e,$00,$01,$40,$00
	.byte	$01,$3e,$00,$01,$3c,$00,$01,$3a,$00
	.byte	$02,$38,$00,$01,$36,$00,$01,$34,$00
	.byte	$01,$32,$00,$01,$30,$00,$02,$2e,$00
	.byte	$01,$2c,$00,$01,$2a,$00,$01,$28,$00
	.byte	$01,$26,$00,$02,$24,$00,$01,$22,$00
	.byte	$01,$20,$00,$01,$1e,$00,$01,$1c,$00
	.byte	$02,$1a,$00,$01,$18,$00,$01,$16,$00
	.byte	$01,$14,$00,$01,$12,$00,$02,$10,$00
	.byte	$01,$0e,$00,$01,$0c,$00,$01,$0a,$00
	.byte	$01,$08,$00,$02,$06,$00,$01,$04,$00
	.byte	$01,$02,$00,$01,$00,$00,$01,$fe,$ff
	.byte	$02,$fc,$ff,$01,$fa,$ff,$01,$f8,$ff
	.byte	$01,$f6,$ff,$01,$f4,$ff,$02,$f2,$ff
	.byte	$01,$f0,$ff,$01,$ee,$ff,$01,$ec,$ff
	.byte	$01,$ea,$ff,$02,$e8,$ff,$01,$e6,$ff
	.byte	$01,$e4,$ff,$01,$e2,$ff,$01,$e0,$ff
	.byte	$02,$de,$ff,$01,$dc,$ff,$01,$da,$ff
	.byte	$01,$d8,$ff,$01,$d6,$ff,$02,$d4,$ff
	.byte	$01,$d2,$ff,$01,$d0,$ff,$01,$ce,$ff
	.byte	$01,$cc,$ff,$02,$ca,$ff,$01,$c8,$ff
	.byte	$01,$c6,$ff,$01,$c4,$ff,$01,$c2,$ff
	.byte	$02,$c0,$ff,$01,$be,$ff,$01,$bc,$ff
	.byte	$01,$ba,$ff,$01,$b8,$ff,$02,$b6,$ff
	.byte	$01,$b4,$ff,$01,$b2,$ff,$01,$b0,$ff
	.byte	$01,$ae,$ff,$02,$ac,$ff,$01,$aa,$ff
	.byte	$01,$a8,$ff,$01,$a6,$ff,$01,$a4,$ff
	.byte	$02,$a2,$ff,$01,$a0,$ff,$01,$a2,$ff
	.byte	$01,$a4,$ff,$01,$a6,$ff,$02,$a8,$ff
	.byte	$01,$aa,$ff,$01,$ac,$ff,$01,$ae,$ff
	.byte	$01,$b0,$ff,$02,$b2,$ff,$01,$b4,$ff
	.byte	$01,$b6,$ff,$01,$b8,$ff,$01,$ba,$ff
	.byte	$02,$bc,$ff,$01,$be,$ff,$01,$c0,$ff
	.byte	$01,$c2,$ff,$01,$c4,$ff,$02,$c6,$ff
	.byte	$01,$c8,$ff,$01,$ca,$ff,$01,$cc,$ff
	.byte	$01,$ce,$ff,$02,$d0,$ff,$01,$d2,$ff
	.byte	$01,$d4,$ff,$01,$d6,$ff,$01,$d8,$ff
	.byte	$02,$da,$ff,$01,$dc,$ff,$01,$de,$ff
	.byte	$01,$e0,$ff,$01,$e2,$ff,$02,$e4,$ff
	.byte	$01,$e6,$ff,$01,$e8,$ff,$01,$ea,$ff
	.byte	$01,$ec,$ff,$02,$ee,$ff,$01,$f0,$ff
	.byte	$01,$f2,$ff,$01,$f4,$ff,$01,$f6,$ff
	.byte	$01,$f8,$ff,$01,$fa,$ff,$01,$fc,$ff
	.byte	$01,$fe,$ff,$01,$00,$00,$00,$02,$00
	.byte	$00,$01,$02,$00,$01,$04,$00,$01,$06
	.byte	$00,$01,$08,$00,$02,$0a,$00,$01,$0c
	.byte	$00,$01,$0e,$00,$01,$10,$00,$01,$12
	.byte	$00,$02,$14,$00,$01,$16,$00,$01,$18
	.byte	$00,$01,$1a,$00,$01,$1c,$00,$02,$1e
	.byte	$00,$01,$20,$00,$01,$22,$00,$01,$24
	.byte	$00,$01,$26,$00,$02,$28,$00,$01,$2a
	.byte	$00,$01,$2c,$00,$01,$2e,$00,$01,$30
	.byte	$00,$02,$32,$00,$01,$34,$00,$01,$36
	.byte	$00,$01,$38,$00,$01,$3a,$00,$02,$3c
	.byte	$00,$01,$3e,$00,$01,$40,$00,$01,$3e
	.byte	$00,$01,$3c,$00,$02,$3a,$00,$01,$38
	.byte	$00,$01,$36,$00,$01,$34,$00,$01,$32
	.byte	$00,$02,$30,$00,$01,$2e,$00,$01,$2c
	.byte	$00,$01,$2a,$00,$01,$28,$00,$02,$26
	.byte	$00,$01,$24,$00,$01,$22,$00,$01,$20
	.byte	$00,$01,$1e,$00,$02,$1c,$00,$01,$1a
	.byte	$00,$01,$18,$00,$01,$16,$00,$01,$14
	.byte	$00,$02,$12,$00,$01,$10,$00,$01,$0e
	.byte	$00,$01,$0c,$00,$01,$0a,$00,$02,$08
	.byte	$00,$01,$06,$00,$01,$04,$00,$01,$02
	.byte	$00,$01,$00,$00,$02,$fe,$ff,$01,$fc
	.byte	$ff,$01,$fa,$ff,$01,$f8,$ff,$01,$f6
	.byte	$ff,$02,$f4,$ff,$01,$f2,$ff,$01,$f0
	.byte	$ff,$01,$ee,$ff,$01,$ec,$ff,$02,$ea
	.byte	$ff,$01,$e8,$ff,$01,$e6,$ff,$01,$e4
	.byte	$ff,$01,$e2,$ff,$02,$e0,$ff,$01,$de
	.byte	$ff,$01,$dc,$ff,$01,$da,$ff,$01,$d8
	.byte	$ff,$02,$d6,$ff,$01,$d4,$ff,$01,$d2
	.byte	$ff,$01,$d0,$ff,$01,$ce,$ff,$02,$cc
	.byte	$ff,$01,$ca,$ff,$01,$c8,$ff,$01,$c6
	.byte	$ff,$01,$c4,$ff,$02,$c2,$ff,$01,$c0
	.byte	$ff,$01,$be,$ff,$01,$bc,$ff,$01,$ba
	.byte	$ff,$02,$b8,$ff,$01,$b6,$ff,$01,$b4
	.byte	$ff,$01,$b2,$ff,$01,$b0,$ff,$02,$ae
	.byte	$ff,$01,$ac,$ff,$01,$aa,$ff,$01,$a8
	.byte	$ff,$01,$a6,$ff,$02,$a4,$ff,$01,$a2
	.byte	$ff,$01,$a0,$ff,$01,$9e,$ff,$01,$9c
	.byte	$ff,$02,$9a,$ff,$01,$98,$ff,$01,$96
	.byte	$ff,$01,$94,$ff,$01,$92,$ff,$02,$90
	.byte	$ff,$01,$8e,$ff,$01,$8c,$ff,$01,$8a
	.byte	$ff,$01,$88,$ff,$02,$86,$ff,$01,$84
	.byte	$ff,$01,$82,$ff,$01,$80,$ff,$01,$7e
	.byte	$ff,$02,$7c,$ff,$01,$7a,$ff,$01,$78
	.byte	$ff,$01,$76,$ff,$01,$74,$ff,$02,$72
	.byte	$ff,$01,$70,$ff,$01,$72,$ff,$01,$74
	.byte	$ff,$01,$76,$ff,$01,$78,$ff,$02,$7a
	.byte	$ff,$01,$7c,$ff,$01,$7e,$ff,$01,$80
	.byte	$ff,$01,$82,$ff,$02,$84,$ff,$01,$86
	.byte	$ff,$01,$88,$ff,$01,$8a,$ff,$01,$8c
	.byte	$ff,$02,$8e,$ff,$01,$90,$ff,$01,$92
	.byte	$ff,$01,$94,$ff,$01,$96,$ff,$02,$98
	.byte	$ff,$01,$9a,$ff,$01,$9c,$ff,$01,$9e
	.byte	$ff,$01,$a0,$ff,$02,$a2,$ff,$01,$a4
	.byte	$ff,$01,$a6,$ff,$01,$a8,$ff,$01,$aa
	.byte	$ff,$02,$ac,$ff,$01,$ae,$ff,$01,$b0
	.byte	$ff,$01,$b2,$ff,$01,$b4,$ff,$02,$b6
	.byte	$ff,$01,$b8,$ff,$01,$ba,$ff,$01,$bc
	.byte	$ff,$01,$be,$ff,$02,$c0,$ff,$01,$00
	.byte	$00,$00
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
HDMA_M7C_END:
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;


;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
LIST_BG1VOFS:
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
	.byte	$82,$86,$8a,$8e,$92,$96,$9a,$9e
	.byte	$a2,$a5,$a9,$ad,$b1,$b4,$b8,$bb
	.byte	$bf,$c2,$c6,$c9,$cc,$cf,$d2,$d5
	.byte	$d8,$db,$de,$e1,$e3,$e6,$e8,$ea
	.byte	$ec,$ee,$f0,$f2,$f4,$f5,$f7,$f8
	.byte	$f9,$fa,$fb,$fc,$fd,$fe,$fe,$ff
	.byte	$ff,$ff,$ff,$ff,$ff,$fe,$fe,$fd
	.byte	$fc,$fb,$fa,$f9,$f8,$f7,$f5,$f4
	.byte	$f2,$f0,$ee,$ec,$ea,$e8,$e6,$e3
	.byte	$e1,$de,$db,$d8,$d5,$d2,$cf,$cc
	.byte	$c9,$c6,$c2,$bf,$bb,$b8,$b4,$b1
	.byte	$ad,$a9,$a5,$a2,$9e,$9a,$96,$92
	.byte	$8e,$8a,$86,$82,$7e,$7a,$76,$72
	.byte	$6e,$6a,$66,$62,$5e,$5b,$57,$53
	.byte	$4f,$4c,$48,$45,$41,$3e,$3a,$37
	.byte	$34,$31,$2e,$2b,$28,$25,$22,$1f
	.byte	$1d,$1a,$18,$16,$14,$12,$10,$0e
	.byte	$0c,$0b,$09,$08,$07,$06,$05,$04
	.byte	$03,$02,$02,$01,$01,$01,$01,$01
	.byte	$01,$02,$02,$03,$04,$05,$06,$07
	.byte	$08,$09,$0b,$0c,$0e,$10,$12,$14
	.byte	$16,$18,$1a,$1d,$1f,$22,$25,$28
	.byte	$2b,$2e,$31,$34,$37,$3a,$3e,$41
	.byte	$45,$48,$4c,$4f,$53,$57,$5b,$5e
	.byte	$62,$66,$6a,$6e,$72,$76,$7a,$7e
	.byte	$00
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;


;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
LIST_M7C:
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
	.byte	$11,$12,$12,$13,$13,$14,$14,$15
	.byte	$15,$16,$16,$17,$17,$18,$18,$18
	.byte	$19,$19,$1a,$1a,$1b,$1b,$1b,$1c
	.byte	$1c,$1c,$1d,$1d,$1d,$1e,$1e,$1e
	.byte	$1f,$1f,$1f,$1f,$20,$20,$20,$20
	.byte	$20,$20,$21,$21,$21,$21,$21,$21
	.byte	$21,$21,$21,$21,$21,$21,$21,$21
	.byte	$21,$21,$20,$20,$20,$20,$20,$20
	.byte	$1f,$1f,$1f,$1f,$1e,$1e,$1e,$1d
	.byte	$1d,$1d,$1c,$1c,$1c,$1b,$1b,$1b
	.byte	$1a,$1a,$19,$19,$18,$18,$18,$17
	.byte	$17,$16,$16,$15,$15,$14,$14,$13
	.byte	$13,$12,$12,$11,$11,$10,$10,$0f
	.byte	$0f,$0e,$0e,$0d,$0d,$0c,$0c,$0b
	.byte	$0b,$0a,$0a,$0a,$09,$09,$08,$08
	.byte	$07,$07,$07,$06,$06,$06,$05,$05
	.byte	$05,$04,$04,$04,$03,$03,$03,$03
	.byte	$02,$02,$02,$02,$02,$02,$01,$01
	.byte	$01,$01,$01,$01,$01,$01,$01,$01
	.byte	$01,$01,$01,$01,$01,$01,$02,$02
	.byte	$02,$02,$02,$02,$03,$03,$03,$03
	.byte	$04,$04,$04,$05,$05,$05,$06,$06
	.byte	$06,$07,$07,$07,$08,$08,$09,$09
	.byte	$0a,$0a,$0a,$0b,$0b,$0c,$0c,$0d
	.byte	$0d,$0e,$0e,$0f,$0f,$10,$10,$11
	.byte	$00
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;


;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
LIST_M7XY:
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
	.byte	$81,$83,$84,$86,$88,$89,$8b,$8c
	.byte	$8e,$90,$91,$93,$95,$96,$98,$99
	.byte	$9b,$9c,$9e,$9f,$a1,$a2,$a4,$a5
	.byte	$a6,$a8,$a9,$aa,$ab,$ad,$ae,$af
	.byte	$b0,$b1,$b2,$b3,$b4,$b5,$b6,$b7
	.byte	$b8,$b9,$b9,$ba,$bb,$bb,$bc,$bd
	.byte	$bd,$be,$be,$be,$bf,$bf,$bf,$c0
	.byte	$c0,$c0,$c0,$c0,$c0,$c0,$c0,$c0
	.byte	$c0,$bf,$bf,$bf,$be,$be,$be,$bd
	.byte	$bd,$bc,$bb,$bb,$ba,$b9,$b9,$b8
	.byte	$b7,$b6,$b5,$b4,$b3,$b2,$b1,$b0
	.byte	$af,$ae,$ad,$ab,$aa,$a9,$a8,$a6
	.byte	$a5,$a4,$a2,$a1,$9f,$9e,$9c,$9b
	.byte	$99,$98,$96,$95,$93,$91,$90,$8e
	.byte	$8c,$8b,$89,$88,$86,$84,$83,$81
	.byte	$7f,$7d,$7c,$7a,$78,$77,$75,$74
	.byte	$72,$70,$6f,$6d,$6b,$6a,$68,$67
	.byte	$65,$64,$62,$61,$5f,$5e,$5c,$5b
	.byte	$5a,$58,$57,$56,$55,$53,$52,$51
	.byte	$50,$4f,$4e,$4d,$4c,$4b,$4a,$49
	.byte	$48,$47,$47,$46,$45,$45,$44,$43
	.byte	$43,$42,$42,$42,$41,$41,$41,$40
	.byte	$40,$40,$40,$40,$40,$40,$40,$40
	.byte	$40,$41,$41,$41,$42,$42,$42,$43
	.byte	$43,$44,$45,$45,$46,$47,$47,$48
	.byte	$49,$4a,$4b,$4c,$4d,$4e,$4f,$50
	.byte	$51,$52,$53,$55,$56,$57,$58,$5a
	.byte	$5b,$5c,$5e,$5f,$61,$62,$64,$65
	.byte	$67,$68,$6a,$6b,$6d,$6f,$70,$72
	.byte	$74,$75,$77,$78,$7a,$7c,$7d,$7f
	.byte	$00
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;


;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
M7PAL:
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
	.incbin	"../mode7gfx/taurus.pal"
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;


;/////////////////////////////////////////////////////////////////////////;
.segment "GRAPHICS"
;/////////////////////////////////////////////////////////////////////////;


;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
M7MAP:	.incbin	"../mode7gfx/taurus.bin"
M7PIC:	.incbin	"../mode7gfx/taurus.pc7"
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
