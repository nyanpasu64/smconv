;-------------------------------------------------------------------------;
.include "copying.inc"
.include "snes.inc"
.include "snes_zvars.inc"
;-------------------------------------------------------------------------;
.export Copy256x256Mode7Map, DMAtoRAM, M7DMAtoVRAM, M7SetMapVRAM
;-------------------------------------------------------------------------;


;/////////////////////////////////////////////////////////////////////////;
.code
;/////////////////////////////////////////////////////////////////////////;

	.a8
	.i16

;*************************************************************************;
; copy data to vram with DMA
;
; a = increment when writing higher byte (00h|80h)
; x = transer size (bytes)
; y = VRAM address (words!)
;*************************************************************************;

;=========================================================================;
M7DMAtoVRAM:
;=========================================================================;
	sta	REG_VMAIN		; increment?
	stx	REG_DAS0L		; set transfer size
	sty	REG_DMAP0		; destination = vram ([8|9]000h)
;---------------------------------------;---------------------------------;
	lda	#7fh			;
	sta	REG_A1B0		; src = REG_A1B0 REG_A1T0L
;---------------------------------------;---------------------------------;
	ldx	#0000h			;
	stx	REG_A1T0L		; src = 7f0000h
	stx	REG_VMADDL		;
;---------------------------------------;---------------------------------;
	sei				;
	lda	#01h			; start transfer
	sta	REG_MDMAEN		;---------------------------------;
	wai
	cli
	rts

;=========================================================================;
M7SetMapVRAM:
;=========================================================================;
	lda	#0			; do not incrent
	ldx	#4000h			; bytes to transfer
	ldy	#1800h			; destination
	rts

;*************************************************************************;
; copy data to RAM with DMA
;	
; a,y = ROM address
; x = size (bytes)
; WMADD = target
;*************************************************************************;
DMAtoRAM:
;-------------------------------------------------------------------------;
	sta	REG_A1B7
	sty	REG_A1T7
	
	stx	REG_DAS7L		; set transfer size
	lda	#<REG_WMDATA		; target = WRAM
	sta	REG_BBAD7		;
	stz	REG_DMAP7		;
	lda	#1<<7			;
	sta	REG_MDMAEN		; start transfer
;-------------------------------------------------------------------------;
	rts


;*************************************************************************;
; Copy M7 gfx & map 256x256 only
;       
; a = bank, assumes both graphics & map are in the same bank
; x = graphics
; y = map
copy_m7_map = m1+1
map_entry = m2
tile_xpos = m3
;*************************************************************************;
Copy256x256Mode7Map:
;-------------------------------------------------------------------------;
	stx	memptr
	sta	memptr+2
	sty	m0
	sta	m1

	lda	#80h
	sta	REG_VMAIN

	ldy	#0000h
	sty	map_entry
	sty	REG_VMADDL
	lda	#1
	sta	copy_m7_map
	stz	tile_xpos
;-------------------------------------------------------------------------;
:	tyx
	jsr	CopyMap
	txy
	lda	[memptr],y
	sta	REG_VMDATAH
	iny
	cpy	#4000h
	bne	:-
;-------------------------------------------------------------------------;
	stz	REG_VMAIN

	ldx	#0000h
	stx	memptr
	stx	m0
	stz	memptr+2
	stx	m1
	stx	map_entry
	stx	tile_xpos

	rts


;=========================================================================;
CopyMap:
;=========================================================================;
	lda	copy_m7_map
	bne	cmp21h
;-------------------------------------------------------------------------;
cmp61h:	inc	tile_xpos
	lda	tile_xpos
	cmp	#61h
	bne	zvmdl
;-------------------------------------------------------------------------;
	stz	tile_xpos
	lda	#01h
	sta	copy_m7_map
;-------------------------------------------------------------------------;
cmp21h:	inc	tile_xpos
	lda	tile_xpos
	cmp	#21h
	bne	cmpmap
;-------------------------------------------------------------------------;
	stz	tile_xpos
	stz	copy_m7_map
	bra	cmp61h
;-------------------------------------------------------------------------;
zvmdl:	stz	REG_VMDATAL
	rts
;-------------------------------------------------------------------------;
cmpmap:	ldy	map_entry
	cpy	#0400h		; only a 256x256 map
	beq	zvmdl
;-------------------------------------------------------------------------;
	lda	[m0],y		; get map data
	sta	REG_VMDATAL
	iny
	sty	map_entry
	rts
;-------------------------------------------------------------------------;
