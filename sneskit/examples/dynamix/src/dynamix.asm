;-------------------------------------------------------------------------;
.include "graphics.inc"
.include "snes.inc"
.include "snes_decompress.inc"
.include "snes_zvars.inc"
;-------------------------------------------------------------------------;
.importzp frame_ready, joy1_down
;-------------------------------------------------------------------------;
.import ASCIIMAP, oam_hitable, oam_table
;-------------------------------------------------------------------------;
.export DoDynamix
;-------------------------------------------------------------------------;


;-------------------------------------------------------------------------;
BG2GFX = 02000h
BG2MAP = 04000h
BG1GFX = 0c000h
BG1MAP = 0b000h
BG3MAP = 01000h
OAMGFX = 00000h
;-------------------------------------------------------------------------;
SPRITE_PROP = OAM_PRI3|OAM_PAL6
;-------------------------------------------------------------------------;
end_flag	= m0
fi_inidisp	= m0+1
fo_inidisp	= m1
temp		= m7
;-------------------------------------------------------------------------;


;/////////////////////////////////////////////////////////////////////////;
.bss
;/////////////////////////////////////////////////////////////////////////;


;-------------------------------------------------------------------------;
bg1_hofs:
	.res 1
bg1_vofs:
	.res 1
bg2_hsine:
	.res 1
bg2_vsine:
	.res 1
bg3_hofs:
	.res 1
credit_ypos:
	.res 1
palette_index:
	.res 1
palette_timer:
	.res 1
scroll_sine_index:
	.res 1
scroll_textpos:
	.res 2
scroll_tile:
	.res 2
scroll_timer:
	.res 1
sprite_tile:
	.res 1
vmadd:
	.res 2
;-------------------------------------------------------------------------;


;/////////////////////////////////////////////////////////////////////////;
.code
;/////////////////////////////////////////////////////////////////////////;


;-------------------------------------------------------------------------;
	.a8
	.i16
;-------------------------------------------------------------------------;


;:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::;
DoDynamix:
;:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::;
	rep	#10h
	sep	#20h

	DoDecompressDataVram gfx_contactTiles, OAMGFX
	DoDecompressDataVram gfx_16x16_fontTiles, BG1GFX
	DoDecompressDataVram gfx_dynamixTiles, BG2GFX
	DoDecompressDataVram gfx_dynamixMap, BG2MAP
	DoCopyPalette gfx_16x16_fontPal, 0, 7
	DoCopyPalette gfx_dynamixPal, 16, 16

	sep	#30h

	jsr	Setup

	lda	#NMI_ON|NMI_JOYPAD
	sta	REG_NMITIMEN

	lda	#01h
	sta	frame_ready

;-------------------------------------------------------------------------;
MainLoop:
;-------------------------------------------------------------------------;
	jsr	ReadNMI
	jsr	FadeIn
	jsr	MoveBG2
	jsr	UpdateScrollText
	jsr	MoveScrolltextV
	jsr	MoveScrolltextH
	jsr	MoveCreditSprites
	jsr	Joypad
	jsr	ChangeContactSprite
	jsr	ContactSpriteFade
	bra	MainLoop
;-------------------------------------------------------------------------;

;=========================================================================;
ReadNMI:
;=========================================================================;
	lda	REG_RDNMI
	and	#80h
	beq	ReadNMI
	lda	REG_RDNMI
	rts

;=========================================================================;
FadeIn:
;=========================================================================;
	lda	fi_inidisp
	cmp	#10h
	bcs	:+
;-------------------------------------------------------------------------;
	sta	REG_INIDISP
	inc	fi_inidisp
;-------------------------------------------------------------------------;
:	rts

;=========================================================================;
ContactSpriteFade:
;=========================================================================;
	inc	palette_timer
	bne	:-
;-------------------------------------------------------------------------;
	lda	#0fch
	sta	palette_timer
	lda	#0e2h
	sta	REG_CGADD
	ldx	palette_index
	lda	CONTACT_PAL,x
	sta	REG_CGDATA
	inx
	lda	CONTACT_PAL,x
	sta	REG_CGDATA
	inc	palette_index
	inc	palette_index
	lda	palette_index
	cmp	#22h
	bne	:+
;-------------------------------------------------------------------------;
	stz	palette_index
	stz	palette_timer
	rts
;-------------------------------------------------------------------------;
:	cmp	#12h
	bne	:+
;-------------------------------------------------------------------------;
	lda	sprite_tile
	clc
	eor	#20h
	sta	sprite_tile
;-------------------------------------------------------------------------;
:	rts

;=========================================================================;
ChangeContactSprite:
;=========================================================================;
	lda	sprite_tile
	xba
	ldx	#00h
	ldy	#6*4
;-------------------------------------------------------------------------;
:	lda	SPRITE_XPOS2,x
	clc
	adc	#0ch
	sta	oam_table,y
	iny

	lda	SPRITE_YPOS2,x
	clc
	adc	#0c2h
	sta	oam_table,y
	iny

	phy

	xba
	tay
	lda	SPRITE_TILE2,y
	pha
	iny
	tya
	xba
	pla

	ply

	sta	oam_table,y
	iny

	lda	#SPRITE_PROP
	sta	oam_table,y
	iny

	inx
	cpx	#20h
	bne	:-
;-------------------------------------------------------------------------;
	rts

;=========================================================================;
Joypad:
;=========================================================================;
	lda	end_flag
	bne	:++
;-------------------------------------------------------------------------;
	lda	joy1_down+1
	and	#0f0h
	bne	:+
;-------------------------------------------------------------------------;
	rts
;-------------------------------------------------------------------------;
:	inc	end_flag
;-------------------------------------------------------------------------;
:	lda	fo_inidisp
	beq	:+
;-------------------------------------------------------------------------;
	wai
	wai
	sta	REG_INIDISP
	dec	fo_inidisp

	cld
	clc

	lda	fo_inidisp+1
	adc	#10h
	sta	REG_MOSAIC
	sta	fo_inidisp+1

	rts
;-------------------------------------------------------------------------;
:	lda	#8fh
	sta	REG_INIDISP
	jmp	DoDynamix

;=========================================================================;
MoveCreditSprites:
;=========================================================================;
	lda	credit_ypos
	xba
	ldy	#00h
	tyx
;-------------------------------------------------------------------------;
:	lda	SPRITE_XPOS,y
	sta	oam_table,x
	inx

	phx

	xba
	tax
	lda	SPRITE_YPOS,x
	xba
	txa
	sec
	sbc	#04h
	xba

	plx

	sta	oam_table,x
	inx

	lda	SPRITE_TILE,y
	sta	oam_table,x
	inx

	lda	#SPRITE_PROP
	sta	oam_table,x
	inx
	iny
	cpy	#06h
	bne	:-
;-------------------------------------------------------------------------;
	inc	credit_ypos
	lda	credit_ypos
	cmp	#38h
	bne	:+
	lda	#18h
	sta	credit_ypos
;-------------------------------------------------------------------------;
:	rts

;=========================================================================;
MoveScrolltextV:
;=========================================================================;
	lda	#20h
	sta	REG_VMADDL
	lda	#(BG3MAP>>9)
	sta	REG_VMADDH
	ldy	#10h
	ldx	scroll_sine_index
;-------------------------------------------------------------------------;
:	lda	SCROLL_SINE,x
	clc
	adc	#58h
	sta	REG_VMDATAL
	lda	#20h
	sta	REG_VMDATAH
	lda	SCROLL_SINE,x
	clc
	adc	#58h
	sta	REG_VMDATAL
	lda	#20h
	sta	REG_VMDATAH
	inx
	inx
	inx
	inx
	inx
	inx
	inx
	inx
	dey
	bne	:-
;-------------------------------------------------------------------------;
	inc	scroll_sine_index
	rts

;=========================================================================;
MoveScrolltextH:
;=========================================================================;
	lda	bg3_hofs
	clc
	adc	#01h
	sta	bg3_hofs
	sta	REG_BG3HOFS
	stz	REG_BG3HOFS
	lda	bg1_hofs
	clc
	adc	#01h
	sta	bg1_hofs
	sta	REG_BG1HOFS
	lda	bg1_vofs
	adc	#00h
	sta	bg1_vofs
	sta	REG_BG1HOFS
;-------------------------------------------------------------------------;
:	rts

;=========================================================================;
UpdateScrollText:
;=========================================================================;
	inc	scroll_timer
	lda	scroll_timer
	cmp	#10h
	bne	:-
;-------------------------------------------------------------------------;
	stz	scroll_timer

	rep	#30h

	ldx	scroll_textpos
	inc	scroll_textpos
	lda	#0000h

	sep	#20h

	lda	SCROLLTEXT,x
	bne	:+
	stz	scroll_textpos
	stz	scroll_textpos+1
;-------------------------------------------------------------------------;
:	cmp	#60h
	bcc	:+
;-------------------------------------------------------------------------;
	and	#5fh
;-------------------------------------------------------------------------;
:	sec
	sbc	#20h
	tax

	lda	ASCIIMAP,x
	sta	scroll_tile

	rep	#30h

	lda	vmadd
	sta	REG_VMADDL

	lda	scroll_tile
	sta	REG_VMDATAL
	ina
	sta	REG_VMDATAL

	lda	vmadd
	clc
	adc	#0020h
	sta	REG_VMADDL

	lda	scroll_tile
	clc
	adc	#10h
	sta	REG_VMDATAL
	ina
	sta	REG_VMDATAL

	sep	#30h

	inc	vmadd
	inc	vmadd
	lda	vmadd
	cmp	#20h
	bne	:+
;-------------------------------------------------------------------------;
	stz	vmadd
	lda	vmadd+1
	eor	#04h
	sta	vmadd+1
;-------------------------------------------------------------------------;
:	rts

;=========================================================================;
MoveBG2:
;=========================================================================;
	ldx	bg2_vsine
	lda	BG2VOFS,x
	sta	REG_BG2VOFS
	stz	REG_BG2VOFS
	inc	bg2_vsine
	lda	bg2_vsine
	cmp	#20h
	bne	:+
;-------------------------------------------------------------------------;
	stz	bg2_vsine
;-------------------------------------------------------------------------;
:	ldx	bg2_hsine
	lda	BG2HOFS,x
	clc
	adc	#56h
	sta	REG_BG2HOFS
	stz	REG_BG2HOFS
	inc	bg2_hsine
	rts

;=========================================================================;
Setup:
;=========================================================================;
	rep	#10h
	sep	#20h

	lda	#80h
	sta	REG_VMAIN
	lda	#02h
	sta	REG_CGSWSEL
	lda	#%110001
	sta	REG_CGADSUB
	lda	#0e0h
	sta	REG_COLDATA
	stz	bg2_vsine
	stz	scroll_timer

	ldx	#0f0fh
	stx	fo_inidisp
	ldx	#0000h
	stx	scroll_textpos
	ldx	#5412h
	stx	vmadd

	stz	bg1_hofs
	stz	bg1_vofs
	stz	bg2_hsine
	stz	end_flag
	stz	fi_inidisp
	stz	palette_index
	stz	palette_timer
	stz	scroll_sine_index
	stz	sprite_tile
	stz	temp

	lda	#08h
	sta	bg3_hofs
	lda	#18h
	sta	credit_ypos

	lda	#0e8h
	sta	temp+1

	lda	#BGMODE_2
	sta	REG_BGMODE
	lda	#BG1MAP>>9-07h
	sta	REG_BG1SC
	lda	#BG2MAP>>9
	sta	REG_BG2SC
	lda	#BG2GFX>>9+BG1GFX>>13
	sta	REG_BG12NBA
	lda	#TM_OBJ
	sta	REG_TM
	lda	#TM_BG2|TM_BG1
	sta	REG_TS
	lda	#0ffh
	sta	REG_BG1HOFS
	stz	REG_BG1HOFS
	lda	#18h
	sta	REG_BG1VOFS
	stz	REG_BG1VOFS
	lda	#0abh
	sta	REG_BG2HOFS
	sta	REG_BG2HOFS
	lda	#0fdh
	sta	REG_BG2VOFS
	stz	REG_BG2VOFS
	lda	#BG3MAP>>9
	sta	REG_BG3SC
	stz	REG_BG3HOFS
	stz	REG_BG3HOFS
	stz	REG_BG3VOFS
	stz	REG_BG3VOFS
	stz	REG_MOSAIC

	stz	REG_OBSEL
	lda	#225
	sta	REG_CGADD
	lda	#94h
	sta	REG_CGDATA
	lda	#52h
	sta	REG_CGDATA
	lda	#80h
	sta	REG_CGDATA
	lda	#7ah
	sta	REG_CGDATA
	lda	#239
	sta	REG_CGADD
	stz	REG_CGDATA
	stz	REG_CGDATA
	stz	REG_OAMADDL
	stz	REG_OAMADDH

	rep	#10h

	ldx	#0220h
;-------------------------------------------------------------------------;
:	stz	REG_OAMDATA
	dex
	bne	:-
;-------------------------------------------------------------------------;
	ldy	#37*4
;-------------------------------------------------------------------------;
:	lda	temp
	sta	oam_table,y
	iny
	lda	temp+1
	sta	oam_table,y
	iny
	lda	#04h
	sta	oam_table,y
	iny
	lda	#OAM_PRI3
	sta	oam_table,y
	iny
	lda	temp+1
	clc
	adc	#08h
	sta	temp+1
	cmp	#0c0h
	bne	:-
;-------------------------------------------------------------------------;
	lda	temp
	cmp	#0f8h
	beq	:+
;-------------------------------------------------------------------------;
	lda	#0f8h
	sta	temp
	bra	:-
;-------------------------------------------------------------------------;
:	sep	#30h
	rts


;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
SCROLLTEXT:
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
	.byte	".............yo!"
	.byte	"    dynamix this"
	.byte	" time deliver yo"
	.byte	"u:      equinox "
	.byte	"from sony images"
	.byte	"oft !           "
	.byte	"supplied by cana"
	.byte	"da's finest!   ."
	.byte	"......thanx to g"
	.byte	"eggin/censor for"
	.byte	" the intro! ... "
	.byte	"  to get in cont"
	.byte	"act with us,or f"
	.byte	"or the latest co"
	.byte	"nsole releases ."
	.byte	"... call one of "
	.byte	"our boards!  bbs"
	.byte	" list is coming "
	.byte	"up now:         "
	.byte	"terminal frost ("
	.byte	"whq)    wild sty"
	.byte	"le (ehq)    rfo "
	.byte	"(chq)    the jam"
	.byte	" (mhq)    the ma"
	.byte	"nsion (ukhq)    "
	.byte	"ice cream (gdrhq"
	.byte	")    innocence ("
	.byte	"ghq)    sindrome"
	.byte	" (hype-hq!) ...."
	.byte	"greets to premie"
	.byte	"re, papillon,lyn"
	.byte	"ch mob and to ki"
	.byte	"llerete ",$22, "me so s"
	.byte	"orry!",$22, " -ballz!  "
	.byte	"                "
	.byte	"---------- scrol"
	.byte	"l is about to re"
	.byte	"start right now."
	.byte	"... ----------  "
	.byte	"             ",0
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;


;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
BG2HOFS:
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
	.byte	$55,$57,$59,$5b,$5d,$5f,$61,$63
	.byte	$64,$66,$67,$68,$6a,$6a,$6b,$6c
	.byte	$6c,$6c,$6c,$6c,$6b,$6b,$6a,$68
	.byte	$67,$65,$64,$62,$60,$5d,$5b,$58
	.byte	$55,$52,$4f,$4c,$48,$45,$41,$3e
	.byte	$3a,$36,$33,$2f,$2b,$28,$24,$21
	.byte	$1d,$1a,$17,$14,$11,$0f,$0c,$0a
	.byte	$08,$06,$05,$03,$02,$01,$01,$00
	.byte	$00,$00,$01,$01,$02,$03,$05,$06
	.byte	$08,$0a,$0c,$0f,$11,$14,$17,$1a
	.byte	$1d,$21,$24,$28,$2b,$2f,$33,$36
	.byte	$3a,$3e,$41,$45,$48,$4c,$4f,$52
	.byte	$55,$58,$5b,$5d,$60,$62,$64,$65
	.byte	$67,$68,$6a,$6b,$6b,$6c,$6c,$6c
	.byte	$6c,$6c,$6b,$6a,$6a,$68,$67,$66
	.byte	$64,$63,$61,$5f,$5d,$5b,$59,$57
	.byte	$55,$53,$51,$4f,$4d,$4b,$49,$47
	.byte	$46,$44,$43,$42,$40,$40,$3f,$3e
	.byte	$3e,$3e,$3e,$3e,$3f,$3f,$40,$42
	.byte	$43,$45,$46,$48,$4a,$4d,$4f,$52
	.byte	$55,$58,$5b,$5e,$62,$65,$69,$6c
	.byte	$70,$74,$77,$7b,$7f,$82,$86,$89
	.byte	$8d,$90,$93,$96,$99,$9b,$9e,$a0
	.byte	$a2,$a4,$a5,$a7,$a8,$a9,$a9,$aa
	.byte	$aa,$aa,$a9,$a9,$a8,$a7,$a5,$a4
	.byte	$a2,$a0,$9e,$9b,$99,$96,$93,$90
	.byte	$8d,$89,$86,$82,$7f,$7b,$77,$74
	.byte	$70,$6c,$69,$65,$62,$5e,$5b,$58
	.byte	$55,$52,$4f,$4d,$4a,$48,$46,$45
	.byte	$43,$42,$40,$3f,$3f,$3e,$3e,$3e
	.byte	$3e,$3e,$3f,$40,$40,$42,$43,$44
	.byte	$46,$47,$49,$4b,$4d,$4f,$51,$53
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;


;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
BG2VOFS:
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
	.byte	$10,$13,$16,$19,$1b,$1d,$1f,$20
	.byte	$20,$20,$1f,$1d,$1b,$19,$16,$13
	.byte	$10,$0d,$0a,$07,$05,$03,$01,$00
	.byte	$00,$00,$01,$03,$05,$07,$0a,$0d
	.byte	$00,$00,$00,$00,$00,$00,$00,$00
	.byte	$00,$00,$00,$00,$00,$00,$00,$00
	.byte	$00,$00,$00,$00,$00,$00,$00,$00
	.byte	$00,$00,$00,$00,$00,$00,$00,$00
	.byte	$00,$00,$80,$7a,$c0,$6a,$c0,$52
	.byte	$40,$3a,$c0,$21,$40,$11,$c0,$08
	.byte	$40,$00,$00,$00,$40,$00,$c0,$08
	.byte	$40,$11,$c0,$21,$40,$3a,$c0,$52
	.byte	$c0,$6a,$80,$7a,$00,$00,$96,$7b
	.byte	$de,$7b,$0c,$7b,$1c,$63,$da,$39
	.byte	$5a,$4a,$58,$29,$48,$62,$02,$31
	.byte	$c4,$49,$80,$18,$d4,$20,$0c,$00
	.byte	$52,$18,$08,$00,$00,$00,$00,$00
	.byte	$00,$00,$00,$00,$00,$00,$00,$00
	.byte	$00,$00,$00,$00,$00,$00,$00,$00
	.byte	$00,$00,$00,$00,$00,$00,$00,$00
	.byte	$00,$00,$00,$00,$01,$00,$01,$03
	.byte	$03,$00,$03,$01,$02,$00,$00,$02
	.byte	$01,$02,$00,$01,$01,$00,$02,$00
	.byte	$02,$00,$02,$00,$03,$00,$03,$00
	.byte	$03,$00,$01,$00,$80,$40,$e0,$90
	.byte	$30,$08,$f8,$00,$f8,$04,$fc,$00
	.byte	$fc,$00,$f8,$04,$c0,$00,$70,$00
	.byte	$f8,$00,$f8,$00,$fc,$00,$fc,$00
	.byte	$fc,$00,$fc,$00,$04,$05,$07,$18
	.byte	$0e,$10,$04,$09,$04,$08,$00,$00
	.byte	$00,$00,$00,$00,$0b,$00,$17,$00
	.byte	$1f,$00,$0f,$00,$0c,$00,$00,$00
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;


;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
SPRITE_XPOS:
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
	.byte	$cb,$d2,$d9,$e0,$e6,$ec
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;


;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
SPRITE_YPOS:
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
	.byte	$c1,$c1,$c2,$c2,$c3,$c4,$c6,$c7
	.byte	$c9,$cb,$cd,$cf,$d1,$d4,$d6,$d8
	.byte	$d6,$d4,$d2,$cf,$cd,$cb,$c9,$c8
	.byte	$c6,$c5,$c3,$c2,$c2,$c1,$c1,$c1
	.byte	$c1,$c1,$c2,$c2,$c3,$c4,$c6,$c7
	.byte	$c9,$cb,$cd,$cf,$d1,$d4,$d6,$d8
	.byte	$d6,$d4,$d2,$cf,$cd,$cb,$c9,$c8
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;


;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
SPRITE_TILE:
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
	.byte	$00,$01,$00,$00,$02,$03
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;


;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
SPRITE_XPOS2:
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
	.byte	$00,$08,$10,$18,$20,$28,$30,$38
	.byte	$00,$08,$10,$18,$20,$28,$30,$38
	.byte	$00,$08,$10,$18,$20,$28,$30,$38
	.byte	$00,$08,$10,$18,$20,$28,$30,$38
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;

;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
SPRITE_YPOS2:
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
	.byte	$00,$00,$00,$00,$00,$00,$00,$00
	.byte	$08,$08,$08,$08,$08,$08,$08,$08
	.byte	$10,$10,$10,$10,$10,$10,$10,$10
	.byte	$18,$18,$18,$18,$18,$18,$18,$18
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;

;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
SPRITE_TILE2:
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
	.byte	$05,$06,$07,$08,$09,$0a,$0b,$0c
	.byte	$15,$16,$17,$18,$19,$1a,$1b,$1c
	.byte	$25,$26,$27,$28,$29,$2a,$2b,$2c
	.byte	$35,$36,$37,$38,$39,$3a,$3b,$3c
	.byte	$0d,$0e,$0f,$10,$11,$12,$13,$14
	.byte	$1d,$1e,$1f,$20,$21,$22,$23,$24
	.byte	$2d,$2e,$2f,$30,$31,$32,$33,$34
	.byte	$3d,$3e,$3f,$40,$41,$42,$43,$44
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;


;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
SCROLL_SINE:
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
	.byte	$40,$43,$46,$49,$4c,$50,$53,$56
	.byte	$58,$5b,$5e,$61,$64,$66,$69,$6b
	.byte	$6d,$6f,$71,$73,$75,$77,$78,$7a
	.byte	$7b,$7c,$7d,$7e,$7f,$7f,$80,$80
	.byte	$80,$80,$80,$7f,$7f,$7e,$7d,$7c
	.byte	$7b,$7a,$78,$77,$75,$73,$71,$6f
	.byte	$6d,$6b,$69,$66,$64,$61,$5e,$5b
	.byte	$58,$56,$53,$50,$4c,$49,$46,$43
	.byte	$40,$3d,$3a,$37,$34,$30,$2d,$2a
	.byte	$28,$25,$22,$1f,$1c,$1a,$17,$15
	.byte	$13,$11,$0f,$0d,$0b,$09,$08,$06
	.byte	$05,$04,$03,$02,$01,$01,$00,$00
	.byte	$00,$00,$00,$01,$01,$02,$03,$04
	.byte	$05,$06,$08,$09,$0b,$0d,$0f,$11
	.byte	$13,$15,$17,$1a,$1c,$1f,$22,$25
	.byte	$28,$2a,$2d,$30,$34,$37,$3a,$3d
	.byte	$40,$43,$46,$49,$4c,$50,$53,$56
	.byte	$58,$5b,$5e,$61,$64,$66,$69,$6b
	.byte	$6d,$6f,$71,$73,$75,$77,$78,$7a
	.byte	$7b,$7c,$7d,$7e,$7f,$7f,$80,$80
	.byte	$80,$80,$80,$7f,$7f,$7e,$7d,$7c
	.byte	$7b,$7a,$78,$77,$75,$73,$71,$6f
	.byte	$6d,$6b,$69,$66,$64,$61,$5e,$5b
	.byte	$58,$56,$53,$50,$4c,$49,$46,$43
	.byte	$40,$3d,$3a,$37,$34,$30,$2d,$2a
	.byte	$28,$25,$22,$1f,$1c,$1a,$17,$15
	.byte	$13,$11,$0f,$0d,$0b,$09,$08,$06
	.byte	$05,$04,$03,$02,$01,$01,$00,$00
	.byte	$00,$00,$00,$01,$01,$02,$03,$04
	.byte	$05,$06,$08,$09,$0b,$0d,$0f,$11
	.byte	$13,$15,$17,$1a,$1c,$1f,$22,$25
	.byte	$28,$2a,$2d,$30,$34,$37,$3a,$3d
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;


;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
CONTACT_PAL:
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;
	.byte	$80,$7a,$c0,$6a,$c0,$52,$40,$3a
	.byte	$c0,$21,$40,$11,$c0,$08,$40,$00
	.byte	$00,$00,$40,$00,$c0,$08,$40,$11
	.byte	$c0,$21,$40,$3a,$c0,$52,$c0,$6a
	.byte	$80,$7a,$c0,$6a,$c0,$52,$40,$3a
	.byte	$c0,$21,$40,$11,$c0,$08,$40,$00
	.byte	$00,$00,$40,$00,$c0,$08,$40,$11
	.byte	$c0,$21,$40,$3a,$c0,$52,$c0,$6a
;=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=;

